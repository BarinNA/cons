Функция ПолучитьСтруктуруНастроекЗагрузки() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("Период");
	Структура.Вставить("База");
	Структура.Вставить("Организация");
	Структура.Вставить("ВидОтчета");
	
	Возврат Структура;
	
КонецФункции	

Функция ЗагрузитьДанныеОтчета(ПараметрыЗагрузки) Экспорт
	
	Соединение = РаботаСHTTPСервисами.ПолучитьСоединение(ПараметрыЗагрузки.База.Пользователь, ПараметрыЗагрузки.База.Пароль);
	Запрос = РаботаСHTTPСервисами.ПолучитьЗапрос(ПараметрыЗагрузки.База.СтрокаПодключения + "/GetRepsData");	
	
	ТекстЗапроса = ПодготовитьТекстЗапроса(ПараметрыЗагрузки);
	Запрос.УстановитьТелоИзСтроки(ТекстЗапроса);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	РаботаСHTTPСервисами.ПроверитьКодСостояния(Ответ.КодСостояния);
	
	СтруктураОтвета = РаботаСJSON.ПреобразоватьJSONВЗначение(Ответ.ПолучитьТелоКакСтроку(), "Period");
	
	Возврат СтруктураОтвета;
		
Конецфункции

Функция ПодготовитьТекстЗапроса(ПараметрыЗагрузки)
	
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("OrgList", Новый Массив);
	
	СтруктураОрг = Новый Структура;
	СтруктураОрг.Вставить("org_id", РаботаСАналитикой.ПолучитьИдАналитики(ПараметрыЗагрузки.База, 
															ПараметрыЗагрузки.Организация));
															
	СтруктураОрг.Вставить("RepTypes", Новый Массив);															
	
	СтруктураОтчета = Новый Структура;
	СтруктураОтчета.Вставить("RepType", Строка(ПараметрыЗагрузки.ВидОтчета));
	СтруктураОтчета.Вставить("Period",  Новый Структура("StartDate,EndDate", ПараметрыЗагрузки.Период.ДатаНачала,
													ПараметрыЗагрузки.Период.ДатаОкончания));
													
	СтруктураОрг.RepTypes.Добавить(СтруктураОтчета);
	СтруктураЗапроса.OrgList.Добавить(СтруктураОрг);													
	
	ТекстЗапроса = РаботаСJSON.ПреобразоватьЗначениеВJSON(СтруктураЗапроса);
	Возврат ТекстЗапроса;
	
КонецФункции