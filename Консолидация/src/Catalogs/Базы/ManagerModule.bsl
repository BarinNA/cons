
Функция ПолучитьСписокОрганизацийИсточника(База, Логин, Пароль) Экспорт
	
	Соединение = РаботаСHTTPСервисами.ПолучитьСоединение(Логин, Пароль);
	Запрос = РаботаСHTTPСервисами.ПолучитьЗапрос(База.СтрокаПодключения + "/GetOrgList");	
	
	Ответ = Соединение.Получить(Запрос);
	
	РаботаСHTTPСервисами.ПроверитьКодСостояния(Ответ.КодСостояния);
	
	СтрОрг = РаботаСJSON.ПреобразоватьJSONВЗначение(Ответ.ПолучитьТелоКакСтроку());
	
	МассивОрг = Новый Массив;
	
	Для Каждого Орг Из СтрОрг.OrgList Цикл
		
		Аналитика = РаботаСАналитикой.ПолучитьЗначениеАналитикиПоИД(База,"Организация", Орг.id);
		
		Если Аналитика.Пустая() Тогда
			
			СтрНовойАналитики = РаботаСАналитикой.ПолучитьСтруктуруНовойАналитики();
			СтрНовойАналитики.База 			= База;
			СтрНовойАналитики.Наименование 	= Орг.Name;
			СтрНовойАналитики.Тип 			= Орг.Type;
			СтрНовойАналитики.ИдИсточник 	= Орг.id;
			
			Аналитика = РаботаСАналитикой.СоздатьАналитику(СтрНовойАналитики);
			
		КонецЕсли;	
		
		МассивОрг.Добавить(Аналитика);
		
	КонецЦикла;	

	Возврат МассивОрг;

КонецФункции