

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДвиженияКорректировкиIFRS = Движения.Корректировки;
	ДвиженияКорректировкиIFRS.Очистить();
	ДвиженияКорректировкиIFRS.Записывать = Истина;
	
	ТаблицаДвижений = ПодготовитьТаблицуДвиженийIFRS();

	ДвиженияКорректировкиIFRS.Загрузить(ТаблицаДвижений);

КонецПроцедуры

Функция ПодготовитьТаблицуДвиженийIFRS()
	
	ТЗдвижений = Движения.Корректировки.Выгрузить();
	ТЗдвижений.Очистить();
	
	Для Каждого Стр Из КорректировкиPL Цикл
		
		МассивПериодов = СформироватьМассивПериодов(стр.ДатаНачала, Стр.ДатаОкончания);
		
		Для Каждого СтрП из МассивПериодов Цикл
			
			СтрДвижения = ТЗдвижений.Добавить();
			СтрДвижения.Период = стрП;
			СтрДвижения.Регистратор = Ссылка;
			СтрДвижения.Организация = Организация;
			СтрДвижения.Статья      = Стр.Статья;
			СтрДвижения.ВидОтчета   = Перечисления.ВидыОтчетов.PL;
			СтрДвижения.Сумма       = Стр.Сумма;
			
		КонецЦикла;	
		
	КонецЦикла;	
	
	Возврат ТЗдвижений;
	
КонецФункции

Функция СформироватьМассивПериодов(ДатаНачала, ДатаОкончания)
	
	Период = НачалоМесяца(ДатаНачала);
	МассивПериодов = Новый Массив;
	
	Пока Период <= ДатаОкончания Цикл
		
		МассивПериодов.Добавить(Период);		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;	
	
	Возврат МассивПериодов;
	
КонецФункции